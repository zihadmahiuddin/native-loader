plugins {
    id 'java-library'
    id 'maven'
    id 'maven-publish'
}

def versionObj = new Version(major: 0, minor: 7, revision: 1)

archivesBaseName = 'native-loader'
group 'com.github.natanbc'
version "${versionObj}"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    api 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'org.slf4j:slf4j-api:1.7.25'
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

publishing {
    publications {
        library(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            url = uri("https://maven.zihad.dev")
            name = "repository"
            credentials {
                username = getProjectProperty("MAVEN_USERNAME")
                password = getProjectProperty("MAVEN_PASSWORD")
            }
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}

String getProjectProperty(String propertyName) {
    String property;
    if(hasProperty(propertyName)) {
        property = project.properties[propertyName]
    } else {
        property = System.getenv(propertyName) ?: ""
    }
    return property
}

class Version {
    String major, minor, revision

    String toString() {
        "$major.$minor.$revision"
    }
}
